Spring Security supports four ways of disabling a user account. When you attempt to log in, the @UserDetailsService@ implementation creates an instance of @UserDetails@ that uses these accessor methods:

* @isAccountNonExpired()@
* @isAccountNonLocked()@
* @isCredentialsNonExpired()@
* @isEnabled()@

If you use the [s2-quickstart|Scripts] script to create a user domain class, it creates a class with corresponding properties to manage this state.

When an accessor returns @true@ for @accountExpired@, @accountLocked@, or @passwordExpired@ or returns @false@ for @enabled@, a corresponding exception is thrown:

{table}
*Accessor* | *Property* | *Exception*
@isAccountNonExpired()@ | @accountExpired@ | [AccountExpiredException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/AccountExpiredException.html]
@isAccountNonLocked()@ | @accountLocked@ | [LockedException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/LockedException.html]
@isCredentialsNonExpired()@ | @passwordExpired@ | [CredentialsExpiredException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/CredentialsExpiredException.html]
@isEnabled()@ | @enabled@ | [DisabledException|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/DisabledException.html]
{table}

You can configure an exception mapping in @Config.groovy@ to associate a URL to any or all of these exceptions to determine where to redirect after a failure, for example:

{code}
grails.plugins.springsecurity.failureHandler.exceptionMappings = [
   'org.springframework.security.authentication.LockedException':             '/user/accountLocked',
   'org.springframework.security.authentication.DisabledException':           '/user/accountDisabled',
   'org.springframework.security.authentication.AccountExpiredException':     '/user/accountExpired',
   'org.springframework.security.authentication.CredentialsExpiredException': '/user/passwordExpired'
]
{code}

Without a mapping for a particular exception, the user is redirected to the standard login fail page (by default @/login/authfail@), which displays an error message from this table:

{table}
*Property* | *Default*
errors.login.disabled | "Sorry, your account is disabled."
errors.login.expired | "Sorry, your account has expired."
errors.login.passwordExpired | "Sorry, your password has expired."
errors.login.locked | "Sorry, your account is locked."
errors.login.fail | "Sorry, we were not able to find a user with that username and password."
{table}

You can customize these messages by setting the corresponding property in @Config.groovy@, for example:

{code}
grails.plugins.springsecurity.errors.login.locked = "None shall pass."
{code}

You can use this functionality to manually lock a user's account or expire the password, but you can automate the process. For example, use the [Quartz plugin|http://grails.org/plugin/quartz] to periodically expire everyone's password and force them to go to a page where they update it. Keep track of the date when users change their passwords and use a Quartz job to expire their passwords once the password is older than a fixed max age.

h4. User Cache
If the @cacheUsers@ configuration property is set to @true@, Spring Security caches @UserDetails@ instances to save trips to the database. (The default is @false@.) This optimization is minor, because typically only two small queries occur during login -- one to load the user, and one to load the authorities.

If you enable this feature, you must remove any cached instances after making a change that affects login. If you do not remove cached instances, even though a user's account is locked or disabled, logins succeed because the database is bypassed. By removing the cached data, you force at trip to the database to retrieve the latest updates.

Here is a sample Quartz job that demonstrates how to find and disable users with passwords that are too old:

{code}
package com.mycompany.myapp

class ExpirePasswordsJob  {

   static triggers = {
      cron name: 'myTrigger', cronExpression: '0 0 0 * * ?' // midnight daily
   }

   def userCache

   void execute() {

      def users = User.executeQuery(
            'from User u where u.passwordChangeDate <= :cutoffDate',
            [cutoffDate: new Date() - 180])

      for (user in users) {
         // flush each separately so one failure doesn't rollback all of the others
         try {
            user.passwordExpired = true
            user.save(flush: true)
            userCache.removeUserFromCache user.username
         }
         catch (e) {
            log.error "problem expiring password for user $user.username : $e.message", e
         }
      }
   }
}
{code}

